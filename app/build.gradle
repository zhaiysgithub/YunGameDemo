plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.kptech.gamesdkdemo"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        multiDexEnabled true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file('./gamedemo.jks')
            storePassword "abcd1234"
            keyAlias "gamedemo"
            keyPassword "abcd1234"

        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true//删除无用资源
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {
            variant.outputs.each { output ->
                if (project.hasProperty('OUT_APK_DIR')) {
                    def dirpath = OUT_APK_DIR
                    println "dirPath:" + dirpath
                    variant.getPackageApplication().outputDirectory = new File(dirpath)
                }

                def apkname = "${variant.productFlavors[0].name}-${variant.versionName}-${variant.versionCode}-${variant.buildType.name}.apk".toLowerCase()
                apkname = project.hasProperty('OUT_APK_NAME') ? OUT_APK_NAME : apkname
                output.outputFileName = new File(apkname)
                println "apkName:" + apkname
            }
        }

    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 小牛互娱测试
    def xiaoniuEnv = [
            app_icon       : "@mipmap/ic_logo",
            app_icon_round : "@mipmap/ic_logo",
            app_name      : "小牛互娱测试Demo",
            corpkey        : "2VRigIT4WTF3YFu-ff6d47c46ff2d4a1"
    ]

    flavorDimensions "default"

    productFlavors {

        // 读取 version.properties 文件，得到当前配置的版本号
        Properties properties = new Properties()
        InputStream inputStream = project.rootProject.file('version.properties').newDataInputStream()
        properties.load(inputStream)

        xiaoniu {
            applicationIdSuffix ".xiaon"
            manifestPlaceholders = xiaoniuEnv

            versionCode = properties.getProperty('XIAON_VERSION_CODE').toInteger()
            versionName = properties.getProperty('XIAON_VERSION_NAME')

            dimension "default"
        }
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

    implementation ('kptech.game.kit:gamebox-154:1.2.1.2')
}